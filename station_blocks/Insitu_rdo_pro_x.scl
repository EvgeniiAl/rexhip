FUNCTION_BLOCK "InSitu_RDO_PRO_X"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      mb_addr : UInt;
   END_VAR

   VAR_OUTPUT 
      dissolved_oxygen : Real;   // mg/L
      temperature : Real;   // C
      saturation : Real;   // %
      oxygen_partial_pressure : Real;   // mbar
      alarm : Bool;
   END_VAR

   VAR_IN_OUT 
      mb_query : "mb_query";
   END_VAR

   VAR 
      s : "mb_station_udt";
      read1 : Array[1..4] of Struct
         value : Real;
         Parameter_ID : UInt;
         Units_ID : UInt;
         data_quality : UInt;
         "Off line_sentinel_value" : UInt;
         Available_Units : UInt;
      END_STRUCT;
      read2 : Struct
         serial_number : UDInt;
         manufacture_date : Array[1..3] of UInt;
      END_STRUCT;
      device_name : String[32];
   END_VAR

   VAR_TEMP 
      tmp_alarm : Bool;
      c : Int;
   END_VAR


BEGIN
	// InSitu - RDO PRO X
	// 
	// https://in-situ.com/wp-content/uploads/2015/08/
	// RDO_PRO-X_-Titan_Modbus_SDI-12-_Guide.pdf
	
	"mb_station_header"(station := #s, mb_query := #mb_query);
	#mb_query.mb_addr := #mb_addr;
	
	#mb_query.mode := #mb_query.c.read.holding_reg;
	#mb_query(data_addr := 38, data_ptr := #read1);
	#mb_query(data_addr := 9002, data_ptr := #read2);
	#mb_query(data_addr := 9019, data_ptr := #device_name);
	
	#dissolved_oxygen := #read1[1].value;
	#temperature := #read1[2].value;
	#saturation := #read1[3].value;
	#oxygen_partial_pressure := #read1[4].value;
	FOR #c := 1 TO 4 DO
	    #tmp_alarm := #tmp_alarm OR #read1[#c].data_quality <> 0;
	END_FOR;
	#alarm := #tmp_alarm;
	
	"mb_station_footer"(station := #s, mb_query := #mb_query);
	
	(*
	Sensor health table:   
	0  Normal Data Quality
	1  Parameter measured without errors using an expired user 
	   calibration. (User Cal Expired)
	2  Parameter measured without errors using an expired factory 
	   calibration. (Factory Cal Expired)
	3  Parameter measured with error, sentinel value supplied. 
	4  Sensor is warming up, sentinel value supplied.
	5  Parameter measured but does not meet normal quality 
	   criteria. The sensor has sustained moderate damage, or  
	   the recommended lifespan has been reached.
	*)
	
END_FUNCTION_BLOCK

