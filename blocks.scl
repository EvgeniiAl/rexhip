FUNCTION "mb_buffer_handler" : Void
{ S7_Optimized_Access := 'FALSE' }
VERSION : 1.0
   VAR_INPUT 
      action : SInt;
      buffer_db : DB_ANY;
   END_VAR

   VAR_IN_OUT 
      data : Variant;
      bytes : DInt;
   END_VAR

   VAR_TEMP 
      "array" : Array[0..300] of Byte;
      e : Int;
      c : Int;
   END_VAR


BEGIN
	CASE #action OF
	    1:  // Count bytes             
	        #bytes := 0;
	        #e := Serialize(SRC_VARIABLE := #data,
	                        DEST_ARRAY => #array,
	                        POS := #bytes);
	    2:  // Write buffer    
	        #bytes := 0;
	        #e := Serialize(SRC_VARIABLE := #data,
	                        DEST_ARRAY => #array,
	                        POS := #bytes);
	        FOR #c := 0 TO #bytes - 1 DO
	            POKE(area := 16#84,
	                 dbNumber := #buffer_db,
	                 byteOffset := #c,
	                 value := #array[#c]);
	        END_FOR;
	    3:  // Read buffer        
	        IF #bytes = 0 THEN
	            #e := Serialize(SRC_VARIABLE := #data,
	                            DEST_ARRAY => #array,
	                            POS := #bytes);
	        END_IF;
	        FOR #c := 0 TO #bytes - 1 DO
	            #array[#c] := PEEK(area := 16#84,
	                               dbNumber := #buffer_db,
	                               byteOffset := #c);
	        END_FOR;
	        #bytes := 0;
	        #e := Deserialize(SRC_ARRAY := #array,
	                          DEST_VARIABLE => #data,
	                          POS := #bytes);
	END_CASE;
END_FUNCTION

FUNCTION "mb_query" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 1.0
   VAR_INPUT 
      unit : USInt;   // Device addr. (0 to 247)
      "fc" : USInt;   // Function code: 1,2,3,4,5,6,15,16. (siemens mode shifted to 200 - 202)
      d_addr : UDInt;   // Data addr. (raw telegram value, 0-65535)
      d_len : UInt;   // 0=>Auto detect
   END_VAR

   VAR_IN_OUT 
      data : Variant;   // Bytes and bools has to add up to whole word(s)
      mb : "udt_mb";
   END_VAR

   VAR_TEMP 
      c : Int;
      e : Int;
      tmp_len : DInt;
      bytes : DInt;
   END_VAR


BEGIN
	// Lightweight Modbus Abstraction Layer Library.
	// Author:   Ola Bjørnli
	// Version:  0.1-beta_4
	// License:  GPLv2, MIT (pick the one of your need)
	// Web:      http://mb.sn7.no
	//           http://github.com/olab84/sn7mb
	
	#mb.query_above.done := false;
	#mb.query_above.error := false;
	#mb.query_above.insert := false;
	#mb.run.qid_cnt := #mb.run.qid_cnt + 1;
	IF #mb.run.qid_cnt <> #mb.q.qid THEN
	    RETURN;
	END_IF;
	
	IF #d_len = 0 AND #mb.options.buffer_handler THEN // auto
	    "mb_buffer_handler"(action := 1, // count
	                        buffer_db := 0,
	                        data := #data,
	                        bytes := #bytes);
	    CASE #fc OF
	        1, 2, 5, 15:
	            #tmp_len := SHL(IN := #bytes, N := 3);
	        3, 4, 6, 16:
	            #tmp_len := SHR(IN := #bytes, N := 1);
	        200..202:
	            CASE #d_addr OF
	                1..9999, 10001..19999:
	                    #tmp_len := SHL(IN := #bytes, N := 3);
	                30001..39999, 30001..49999, 400001..465535:
	                    #tmp_len := SHR(IN := #bytes, N := 1);
	            END_CASE;
	    END_CASE;
	ELSE
	    #tmp_len := #d_len;
	    #bytes := SHL(IN := #d_len, N := 1); 
	END_IF;
	
	IF #mb.run.insert THEN
	    CASE #fc OF
	        1:  // Read output bits 
	            #mb.run.mode := 0;
	            #mb.run.data_addr := 1 + #d_addr;
	        2:  // Read input bits
	            #mb.run.mode := 0;
	            #mb.run.data_addr := 10001 + #d_addr;
	        3:  // Read holding reg.
	            #mb.run.mode := 0;
	            #mb.run.data_addr := 400001 + #d_addr;
	        4:  // Read input word
	            #mb.run.mode := 0;
	            #mb.run.data_addr := 30001 + #d_addr;
	        5:  // Writing a single output bit
	            #mb.run.mode := 1;
	            #mb.run.data_addr := 1 + #d_addr;
	        6:  // Writing a single holding register
	            #mb.run.mode := 1;
	            #mb.run.data_addr := 400001 + #d_addr;
	        15: // Writing multiple output bits
	            #mb.run.mode := 2;
	            #mb.run.data_addr := 1 + #d_addr;
	        16: // Writing multiple holding registers
	            #mb.run.mode := 2;
	            #mb.run.data_addr := 400001 + #d_addr;
	        200..202: // Siemens mode: 200-202 replaces 0-2
	            #mb.run.mode := #fc - 200;
	            #mb.run.data_addr := #d_addr;
	    END_CASE;
	    #mb.q.unit := #unit;
	    #mb.q.fc := #fc;
	    #mb.q.d_addr := #d_addr;
	    #mb.q.d_len := DINT_TO_UINT(#tmp_len);
	    IF (#mb.run.mode = 1 OR #mb.run.mode = 2)
	        AND #mb.options.buffer_handler
	    THEN
	        "mb_buffer_handler"(
	            action := 2, // write
	            buffer_db := #mb.const.buffer_db,
	            data := #data,
	            bytes := #bytes);
	    END_IF;
	    #mb.run.insert := false;
	    #mb.q.next := #mb.q.qid + 1;
	    #mb.query_above.insert := true;
	    
	ELSIF #mb.status.done OR #mb.status.error THEN
	    IF #mb.q.unit = #unit
	        AND #mb.q.fc = #fc
	        AND #mb.q.d_addr = #d_addr
	        AND #mb.q.d_len = #tmp_len
	    THEN
	        IF #mb.status.done THEN
	            IF #mb.run.mode = 0
	                AND #mb.options.buffer_handler
	            THEN
	                "mb_buffer_handler"(
	                    action := 3, // read
	                    buffer_db := #mb.const.buffer_db,
	                    data := #data,
	                    bytes := #bytes);
	            END_IF;
	            #mb.query_above.done := true;
	        ELSIF #mb.status.error THEN
	            #mb.query_above.error := true;
	        END_IF;
	        #mb.run.exec_cnt := #mb.run.exec_cnt + 1;
	    END_IF;
	    #mb.run.insert := true;
	    #mb.q.qid := #mb.q.next;
	END_IF;
	
	#mb.options.buffer_handler := true;
	
END_FUNCTION

FUNCTION "mb_read" : Void
TITLE = Read holding registers
{ S7_Optimized_Access := 'TRUE' }
VERSION : 1.0
   VAR_INPUT 
      unit : USInt;   // Device addr. (0 to 247)
      d_addr : UInt;   // Data addr. (raw telegram value, 0-65535)
   END_VAR

   VAR_IN_OUT 
      data : Variant;   // Bytes and bools has to add up to whole word(s)
      mb : "udt_mb";
   END_VAR

   VAR_TEMP 
      bytes : DInt;
      len : UInt;
   END_VAR


BEGIN
	IF #mb.run.qid_cnt + 1 = #mb.q.qid THEN
	    "mb_buffer_handler"(
	        action := 1, // count
	        buffer_db := 0,
	        data := #data,
	        bytes := #bytes);
	    #len := DINT_TO_UINT(SHR(IN:= #bytes, N:=1));
	    #mb.options.buffer_handler := false;
	END_IF;
	
	"mb_query"(unit := #unit,
	           fc := 3, // read holding reg.
	           d_addr := UINT_TO_UDINT(#d_addr),
	           d_len := #len, 
	           data := #data,
	           mb := #mb );
	
	IF #mb.query_above.done THEN
	    "mb_buffer_handler"(
	        action := 3, // read
	        buffer_db := #mb.const.buffer_db,
	        data := #data, 
	        bytes := #bytes );
	END_IF;
END_FUNCTION

FUNCTION "mb_device_footer" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 1.0
   VAR_IN_OUT 
      device : "udt_mb_device";
      mb : "udt_mb";
   END_VAR

   VAR_TEMP 
      e : Int;
      direct_err : Bool;
   END_VAR


BEGIN
	#device.internal.range_end_qid := #mb.run.qid_cnt;
	
	IF #mb.run.exec_cnt - #device.internal.tmp_exec_cnt > 0 THEN
	    IF #mb.status.done THEN
	        #device.log.done_cnt := #device.log.done_cnt + 1;
	        #e := RD_LOC_T(#device.log.done_dtl);
	        #device.internal.err_cnt := 0;
	    ELSIF #mb.status.error THEN
	        #device.log.err_cnt := #device.log.err_cnt + 1;
	        #e := RD_LOC_T(#device.log.err_dtl);
	        #device.log.err_status := #mb.status.status;
	        #device.log.err_d_addr := #mb.q.d_addr ;
	        #device.internal.err_cnt := MIN(
	            IN1 := #device.internal.err_cnt + 1,
	            IN2 := 250 );
	        #direct_err := NOT #mb.status.timeout;
	    END_IF;
	END_IF;
	
	#device.qError := #device.enable AND (
	    #device.internal.err_cnt > #device.config.max_err
	    OR #direct_err
	);
END_FUNCTION

FUNCTION "mb_device_header" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 1.0
   VAR_IN_OUT 
      device : "udt_mb_device";
      mb : "udt_mb";
   END_VAR


BEGIN
	#device.internal.range_start_qid := #mb.run.qid_cnt + 1;
	#device.internal.tmp_exec_cnt := #mb.run.exec_cnt;
	
	IF #mb.q.qid >= #device.internal.range_start_qid
	    AND #mb.q.qid <= #device.internal.range_end_qid
	THEN
	    IF NOT #device.enable
	        OR NOT #mb.run.insert AND #mb.status.timeout
	    THEN
	        #mb.run.insert := true;
	        #mb.q.qid := #device.internal.range_end_qid + 1;
	        #device.internal.err_cnt := MIN(
	            IN1 := #device.internal.err_cnt + 1,
	            IN2 := 255);
	    END_IF;
	END_IF;
END_FUNCTION

FUNCTION_BLOCK "mb_rtu"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 1.0
   VAR_INPUT 
      hardware_id : PORT;
      baud : UDInt := 9600;
      parity : Bool;   // enable even parity. false=no parity.
      timeout : Time := T#100ms;
      buffer_db_any : DB_ANY;
   END_VAR

   VAR_IN_OUT 
      buffer_variant : Variant;
      mb : "udt_mb";
   END_VAR

   VAR 
      comm_load {OriginalPartName := 'MB_COMM_LOAD'; LibVersion := '2.1'} : MB_COMM_LOAD;
      master {OriginalPartName := 'MB_MASTER'; LibVersion := '2.2'} : MB_MASTER;
   END_VAR


BEGIN
	#mb.const.app := false;
	
	#comm_load(REQ := NOT #comm_load.DONE
	                AND NOT #master.BUSY,
	           "PORT" := #hardware_id,
	           BAUD := #baud,
	           PARITY := SEL(G := #parity,
	                         IN0 := 0, IN1 := 2),
	           RESP_TO := TIME_TO_UINT( #timeout ),
	           MB_DB := #master );
	
	#master(REQ := #comm_load.DONE
	            AND NOT #master.BUSY
	            AND NOT #mb.run.insert,
	        MB_ADDR := #mb.q.unit,
	        MODE := #mb.run.mode,
	        DATA_ADDR := #mb.run.data_addr,
	        DATA_LEN := #mb.q.d_len,
	        DONE => #mb.status.done,
	        ERROR => #mb.status.error,
	        STATUS => #mb.status.status,
	        DATA_PTR := #buffer_variant );
	
	#mb.status.timeout :=
	#master.ERROR AND #master.STATUS = 16#80C8;
	// ------------------------
	#mb.const.buffer_db := #buffer_db_any;
	#mb.const.queries := #mb.run.qid_cnt;
	IF #mb.q.qid > #mb.run.qid_cnt THEN
	    #mb.q.qid := 1;
	END_IF;
	#mb.run.qid_cnt := 0;
END_FUNCTION_BLOCK

FUNCTION_BLOCK "mb_tcp"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 1.0
   VAR_INPUT 
      interface_id : HW_ANY;
      connection_id : CONN_OUC := 42;
      ip_addr : Array[1..4] of Byte := 192, 168, 1, 0;
      tcp_port : UInt := 502;   // enable even parity
      timeout : Real := 0.5;   // sec
      buffer_db_any : DB_ANY;
   END_VAR

   VAR_IN_OUT 
      buffer_variant : Variant;
      mb : "udt_mb";
   END_VAR

   VAR 
      client {OriginalPartName := 'MB_CLIENT'; LibVersion := '4.0'} : MB_CLIENT;
      tcon_ip_v4 {OriginalPartName := 'TCON_IP_v4'; LibVersion := '1.0'} : TCON_IP_v4;
   END_VAR


BEGIN
	#mb.const.app := true;
	#tcon_ip_v4.InterfaceId := #interface_id;
	#tcon_ip_v4.ID := #connection_id;
	#tcon_ip_v4.RemoteAddress.ADDR := #ip_addr;
	#tcon_ip_v4.RemotePort := #tcp_port;
	
	#client.MB_Unit_ID := #mb.q.unit;
	#client.Rcv_Timeout := #timeout;
	#client(REQ := NOT #client.BUSY AND #client.Connected
	            AND NOT #mb.run.insert,
	        DISCONNECT := #client.DISCONNECT,
	        MB_MODE := #mb.run.mode,
	        MB_DATA_ADDR := #mb.run.data_addr,
	        MB_DATA_LEN := #mb.q.d_len,
	        DONE => #mb.status.done,
	        ERROR => #mb.status.error,
	        STATUS => #mb.status.status,
	        MB_DATA_PTR := #buffer_variant,
	        CONNECT := #tcon_ip_v4);
	    
	#mb.status.timeout := #client.ERROR AND #client.rcv_timer.Q;
	// ------------------------
	#mb.const.buffer_db := #buffer_db_any;
	#mb.const.queries := #mb.run.qid_cnt;
	IF #mb.q.qid > #mb.run.qid_cnt THEN #mb.q.qid := 1; END_IF;
	#mb.run.qid_cnt := 0;
END_FUNCTION_BLOCK

FUNCTION "mb_query_place_holder" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 1.0
   VAR_IN_OUT 
      mb : "udt_mb";
   END_VAR


BEGIN
	#mb.run.qid_cnt := #mb.run.qid_cnt + 1;
	
	IF #mb.run.qid_cnt = #mb.q.qid THEN
	    #mb.q.qid := #mb.q.qid + 1;
	    #mb.run.insert := true;
	    #mb.options.buffer_handler := true;
	    
	    #mb.query_above.done := false;
	    #mb.query_above.error := false;
	    #mb.query_above.insert := false;
	END_IF;
	
END_FUNCTION

FUNCTION "mb_write" : Void
TITLE = Write holding registers
{ S7_Optimized_Access := 'TRUE' }
VERSION : 1.0
   VAR_INPUT 
      unit : USInt;   // Device addr. (0 to 247)
      d_addr : UInt;   // Data addr. (raw telegram value, 0-65535)
   END_VAR

   VAR_IN_OUT 
      data : Variant;   // Bytes and bools has to add up to whole word(s)
      mb : "udt_mb";
   END_VAR

   VAR_TEMP 
      bytes : DInt;
      len : UInt;
   END_VAR


BEGIN
	IF #mb.run.qid_cnt + 1 = #mb.q.qid THEN
	    "mb_buffer_handler"(
	        action := 2, // write
	        buffer_db := 0,
	        data := #data,
	        bytes := #bytes);
	    #len := DINT_TO_UINT(SHR(IN:=#bytes, N:=1));
	    #mb.options.buffer_handler := false;
	END_IF;
	
	"mb_query"(unit := #unit,
	           fc := 16, // write holding reg.
	           d_addr := UINT_TO_UDINT(#d_addr),
	           d_len := #len,
	           data := #data,
	           mb := #mb);
	
END_FUNCTION

